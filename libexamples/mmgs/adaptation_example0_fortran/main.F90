!> @author
!> Cecile Dobrzynski, Charles Dapogny, Pascal Frey and Algiane Froehly
!> @brief
!>  Example for using mmgslib (basic use)

!> Include the mmgs library hader file
! if the header file is in the "include" directory
! #include "libmmgsf.h"

! if the header file is in "include/mmg/mmgs"
#include "mmg/mmgs/libmmgsf.h"

PROGRAM main

  IMPLICIT NONE

  MMG5_DATA_PTR_T    :: mmgMesh
  MMG5_DATA_PTR_T    :: mmgSol
  INTEGER            :: ier
  CHARACTER(len=255) :: pwd
  CHARACTER(len=300) :: filename

  WRITE(*,*) "  -- TEST MMGSLIB"

  ! Name and path of the mesh file
  CALL getenv("PWD",pwd)
  WRITE(filename,*) TRIM(pwd),"/../libexamples/mmgs/adaptation_example0_fortran/cube"

  !> ------------------------------ STEP   I --------------------------
  !! 1) Initialisation of mesh and sol structures
  !! args of InitMesh: mesh=&mmgMesh, sol=&mmgSol, input mesh name, input sol name,
  !! output mesh name
  mmgMesh = 0
  mmgSol  = 0
  !! Remark: %val(0) allow to pass the value 0 (i.e. NULL) instead of a pointer
  !! toward NULL.
  CALL MMGS_Init_mesh(mmgMesh,mmgSol,%val(0))

  !> 2) Build mesh in MMG5 format
  !! Two solutions: just use the MMGS_loadMesh function that will read a .mesh(b)
  !!    file formatted or manually set your mesh using the MMGS_Set* functions

  !> with MMGS_loadMesh function
  !! a) (not mandatory): give the mesh name
  !!   (by default, the "mesh.mesh" file is oppened)
  CALL MMGS_Set_inputMeshName(mmgMesh,TRIM(ADJUSTL(filename)),&
       LEN(TRIM(ADJUSTL(filename))),ier)
  IF ( ier /= 1 ) THEN
     CALL EXIT(101)
  ENDIF

  !> b) function calling
  CALL MMGS_loadMesh(mmgMesh,ier)
  IF ( ier /= 1 )  CALL EXIT(102)

  !> 3) Build sol in MMG5 format
  !! Two solutions: just use the MMGS_loadSol function that will read a .sol(b)
  !!    file formatted or manually set your sol using the MMGS_Set* functions

  !> With MMGS_loadSol function
  !! a) (not mandatory): give the sol name
  !!   (by default, the "mesh.sol" file is oppened)
  CALL MMGS_Set_inputSolName(mmgMesh,mmgSol,TRIM(ADJUSTL(filename)),&
       LEN(TRIM(ADJUSTL(filename))),ier)
  IF ( ier /= 1 ) THEN
     CALL EXIT(103)
  ENDIF

  !> b) function calling
  CALL MMGS_loadSol(mmgMesh,mmgSol,ier)
  IF ( ier /= 1 ) THEN
     CALL EXIT(104)
  ENDIF

  !> 4) (not mandatory): check if the number of given entities match with mesh size
  CALL MMGS_Chk_meshData(mmgMesh,mmgSol,ier)
  IF ( ier /= 1 ) CALL EXIT(105)

  !> ------------------------------ STEP  II --------------------------
  !! library call
  !! Remark: %val(0) allow to pass the value 0 (i.e. NULL) instead of a pointer
  !! toward NULL.
  CALL MMGS_mmgslib(mmgMesh,mmgSol,ier)
  IF ( ier == MMG5_STRONGFAILURE ) THEN
     PRINT*,"BAD ENDING OF MMGSLIB: UNABLE TO SAVE MESH"
     STOP 2
  ELSE IF ( ier == MMG5_LOWFAILURE ) THEN
     PRINT*,"BAD ENDING OF MMGSLIB"
  ENDIF

  !> ------------------------------ STEP III --------------------------
  !! get results
  !! Two solutions: just use the MMGS_saveMesh/MMGS_saveSol functions
  !!    that will write .mesh(b)/.sol formatted files or manually get your mesh/sol
  !!    using the MMGS_getMesh/MMGS_getSol functions

  !> 1) Automatically save the mesh
  !! a)  (not mandatory): give the ouptut mesh name using MMGS_Set_outputMeshName
  !!   (by default, the mesh is saved in the "mesh.o.mesh" file
  !!call MMGS_Set_outputMeshName(mmgMesh,"output.mesh",len("output.mesh"),ier)
  !! b) function calling
  CALL MMGS_saveMesh(mmgMesh,ier)
  IF ( ier /= 1 ) CALL EXIT(106)

  !> 2) Automatically save the solution
  !! a)  (not mandatory): give the ouptut sol name using MMGS_Set_outputSolName
  !!   (by default, the mesh is saved in the "mesh.o.sol" file
  !!call MMGS_Set_outputSolName(mmgMesh,mmgSol,"output.sol",len("output.sol"),ier)
  !! b) function calling
  CALL MMGS_saveSol(mmgMesh,mmgSol,ier)
  IF ( ier /= 1 ) CALL EXIT(107)

  !> 3) Free the MMGS5 structures
  CALL MMGS_Free_all(mmgMesh,mmgSol,%val(0))

END PROGRAM main
